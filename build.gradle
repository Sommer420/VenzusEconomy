plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'dk.sqmmer'
version = '1.0.0'

java {
    toolchain { languageVersion = JavaLanguageVersion.of(8) }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://maven.enginehub.org/repo/' }
    maven { url { uri("https://storehouse.okaeri.eu/repository/maven-releases/") } }
}

dependencies {
    implementation('org.yaml:snakeyaml:2.2') { version { strictly '2.2' } }

    implementation 'eu.okaeri:okaeri-platform-bukkit:0.4.19'
    implementation 'eu.okaeri:okaeri-commands-bukkit:0.9.5'
    implementation 'eu.okaeri:okaeri-configs-yaml-snakeyaml:5.0.1'

    compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'

    implementation 'org.xerial:sqlite-jdbc:3.46.0.0'

    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'me.clip:placeholderapi:2.11.6'

    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

shadowJar {
    archiveBaseName.set('VenzusEconomy')
    archiveVersion.set(project.version as String)
    archiveClassifier.set('')

    relocate 'eu.okaeri',            'dk.sqmmer.venzuseco.libs.okaeri'
    relocate 'org.yaml.snakeyaml',   'dk.sqmmer.venzuseco.libs.snakeyaml'
    relocate 'org.sqlite',           'dk.sqmmer.venzuseco.libs.sqlite'

    minimize()
}

jar.enabled = false
build.dependsOn(shadowJar)
